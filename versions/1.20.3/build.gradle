plugins {
    id 'fabric-loom'
    id 'maven-publish'
}

version = "${rootProject.mod_version}+mc${project.minecraft_version}"
group = rootProject.maven_group

base {
    archivesName = "${rootProject.archives_base_name}-${project.minecraft_version}"
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "orbital_railgun_enhanced" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }
}

// Configure source sets to use shared source from root project AFTER loom creates them
afterEvaluate {
    sourceSets {
        main {
            java {
                srcDirs = [rootProject.file('src/main/java')]
            }
            resources {
                srcDirs = [rootProject.file('src/main/resources')]
            }
        }
        client {
            java {
                srcDirs = [rootProject.file('src/client/java')]
            }
            resources {
                srcDirs = [rootProject.file('src/client/resources')]
            }
        }
    }
}

repositories {
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        name = 'Ladysnake Mods'
        url = 'https://maven.ladysnake.org/releases'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroup 'org.ladysnake'
            includeGroupByRegex 'dev\\.onyxstudios.*'
        }
    }
    maven {
        name = 'Wispforest'
        url = 'https://maven.wispforest.io'
    }
}

dependencies {
    compileOnly 'org.apiguardian:apiguardian-api:1.1.2'
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "io.wispforest:owo-lib:${project.owo_lib_version}"
    include "io.wispforest:owo-lib:${project.owo_lib_version}"
    annotationProcessor "io.wispforest:owo-lib:${project.owo_lib_version}"
    clientAnnotationProcessor "io.wispforest:owo-lib:${project.owo_lib_version}"

    modImplementation("software.bernie.geckolib:geckolib-fabric-${project.minecraft_version}:${project.geckolib_version}")
    // mclib is a GeckoLib animation library dependency
    implementation("com.eliotlash.mclib:mclib:${rootProject.mclib_version}")

    modImplementation "org.ladysnake:satin:${project.satin_version}"
    include "org.ladysnake:satin:${project.satin_version}"

    implementation "com.google.code.gson:gson:2.10.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", rootProject.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": rootProject.loader_version
    }
}

def targetJavaVersion = project.java_version.toInteger()
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.compilerArgs += ['-Xlint:-processing']
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

jar {
    from(rootProject.file("LICENSE.txt")) {
        rename { "${it}_${rootProject.archives_base_name}" }
    }
}

// Configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = "${rootProject.archives_base_name}-${project.minecraft_version}"
            from components.java
        }
    }
}
